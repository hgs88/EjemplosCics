             CBL CICS('COBOL3') APOST                                   00100000
      ***************************************************************** 00200000
      *                                                               * 00300000
      *  MODULE NAME = DFH0XCUI                                       * 00400000
      *                                                               * 00500000
      *  DESCRIPTIVE NAME = CICS     (Samples) Example Application -  * 00600000
      *                     BMS manager for Web Service Client        * 00700000
      *                                                               * 00800000
      *  @BANNER_START                           01                   * 00816600
      *  Licensed Materials - Property of IBM                         * 00833200
      *                                                               * 00849800
      *  5655-M15              DFH0XCUI                               * 00866400
      *                                                               * 00883000
      *  (C) Copyright IBM Corp. 2005                                 * 00899600
      *                                                               * 00916200
      *  CICS                                                         * 00932800
      *  (Element of CICS Transaction Server                          * 00949400
      *  for z/OS, Version 3 Release 2)                               * 00966000
      *  @BANNER_END                                                  * 00982600
      *                                                               * 01000000
      * STATUS = 6.4.0                                                * 01100000
      *                                                               * 01200000
      *  TRANSACTION NAME = n/a                                       * 01300000
      *                                                               * 01400000
      *  FUNCTION =                                                   * 01500000
      *  This module handles the BMS interactions for the CICS Web    * 01600000
      *  Service client for the Example Application                   * 01700000
      *                                                               * 01800000
      *-------------------------------------------------------------  * 01900000
      *                                                               * 02000000
      *  ENTRY POINT = DFH0XCUI                                       * 02100000
      *                                                               * 02200000
      *-------------------------------------------------------------  * 02300000
      *                                                               * 02400000
      *  CHANGE ACTIVITY :                                            * 02500000
      *                                                               * 02600000
      *       $MOD(DFH0XCUI),COMP(SAMPLES),PROD(CICS    ):            * 02700000
      *                                                               * 02800000
      *   PN= REASON REL YYMMDD HDXXIII : REMARKS                     * 02900000
      *  $D0= I07544 640 050113 HDIPCB  : ExampleApp CICS client code * 03000000
      *  $D1= I07544 640 050208 HDIPCB  : Definitions for Example App * 03100000
      *                                                               * 03200000
       IDENTIFICATION DIVISION.                                         03300000
       PROGRAM-ID. DFH0XCUI.                                            03400000
       ENVIRONMENT DIVISION.                                            03500000
       CONFIGURATION SECTION.                                           03600000
       DATA DIVISION.                                                   03700000
       WORKING-STORAGE SECTION.                                         03800000
      *----------------------------------------------------------------*03900000
      * Common defintions                                              *04000000
      *----------------------------------------------------------------*04100000
      * Run time (debug) infomation for this invocation                 04200000
        01  WS-HEADER.                                                  04300000
           03 WS-EYECATCHER            PIC X(16)                        04400000
                                        VALUE 'DFH0XCUI------WS'.       04500000
           03 WS-TRANSID               PIC X(4).                        04600000
           03 WS-TERMID                PIC X(4).                        04700000
           03 WS-TASKNUM               PIC 9(7).                        04800000
           03 WS-CALEN                 PIC S9(4) COMP.                  04900000
                                                                        05000000
      * Variables for time/date processing                              05100000
       01  ABS-TIME                    PIC S9(8) COMP VALUE +0.         05200000
       01  TIME1                       PIC X(8)  VALUE SPACES.          05300000
       01  DATE1                       PIC X(10) VALUE SPACES.          05400000
                                                                        05500000
      * Error Message structure                                         05600000
       01  ERROR-MSG.                                                   05700000
           03 EM-DATE                  PIC X(8)  VALUE SPACES.          05800000
           03 FILLER                   PIC X     VALUE SPACES.          05900000
           03 EM-TIME                  PIC X(6)  VALUE SPACES.          06000000
           03 FILLER                   PIC X(9)  VALUE ' DFH0XCUI'.     06100000
           03 EM-DETAIL                PIC X(50) VALUE SPACES.          06200000
                                                                        06300000
      * Key into the configuration file                                 06400000
       01 EXAMPLE-APP-CONFIG           PIC X(9)                         06500000
               VALUE 'EXMP-CONF'.                                       06600000
                                                                        06700000
       01 PROGRAM-EXIT-MESSAGE         PIC X(43)                        06800000
               VALUE 'Thank You for using the Catalog Application'.     06900000
                                                                        07000000
       01 CONSTANTS.                                                    07100000
           03 LINK-COMMAREA-LENGTH     PIC S9(4) COMP VALUE 998.        07200000
                                                                        07300000
       01 WORKING-VARIABLES.                                            07400000
           03 WS-LOOP-COUNTER          PIC S9(4) COMP.                  07500000
           03 DATA-VALID-FLAG          PIC X   VALUE '1'.               07600000
               88 DATA-VALID                   VALUE '1'.               07700000
               88 DATA-INVALID                 VALUE '2'.               07800000
                                                                        07900000
      * Working storage copy of the Communication Area                  08000000
       01 WS-COMMAREA.                                                  08100000
           COPY DFH0XCP1.                                               08200000
           03 FLAGS.                                                    08300000
               05 PROCESSING-MAP-FLAG      PIC X   VALUE '1'.           08400000
                   88 PROCESSING-MENU-MAP          VALUE '1'.           08500000
                   88 PROCESSING-INQ-MAP           VALUE '2'.           08600000
                   88 PROCESSING-ORDER-MAP         VALUE '3'.           08700000
               05 ORDER-SELECT-FLAG        PIC X   VALUE '2'.           08800000
                   88 ORDER-REQ-FOUND              VALUE '1'.           08900000
                   88 ORDER-REQ-NOT-FOUND          VALUE '2'.           09000000
               05 DEBUG-FLAG               PIC X   VALUE '1'.           09100000
                   88 DEBUG-ON                     VALUE '1'.           09200000
                   88 DEBUG-OFF                    VALUE '2'.           09300000
               05 INQ-SCROLL-FLAG          PIC X   VALUE '2'.           09400000
                   88 DATA-SCROLLED                VALUE '1'.           09500000
                   88 DATA-NOT-SCROLLED            VALUE '2'.           09600000
                                                                        09700000
           03 SWITCHES.                                                 09800000
               05 SEND-SWITCH              PIC X   VALUE '1'.           09900000
                   88 SEND-ERASE                   VALUE '1'.           10000000
                   88 SEND-DATAONLY                VALUE '2'.           10100000
                   88 SEND-ALARM                   VALUE '3'.           10200000
                                                                        10300000
           03 WS-VARIABLES.                                             10400000
               05 WS-WEBSERVICE-CLIENT-PROG    PIC X(8)                 10500000
                                                   VALUE 'DFH0XECC'.    10600000
               05 WS-ORDER-ITEM-REF            PIC X(4)  VALUE SPACES.  10700000
               05 WS-INQ-START-ITEM-REF        PIC X(4)  VALUE SPACES.  10800000
               05 WS-INQ-ITEM-LIST-DEPTH       PIC S9(4) COMP VALUE 1.  10900000
               05 WS-INQ-ITEM-LIST-CURRENT     PIC S9(4) COMP VALUE 1.  11000000
               05 WS-INQ-QNAME                 PIC X(10) VALUE SPACES.  11100000
                                                                        11200000
                                                                        11300000
       01 DEBUG.                                                        11400000
           03 DEBUG-STRING             PIC X(150)  VALUE SPACES.        11500000
           03 DEBUG-NUMERIC            PIC S9(4)   USAGE DISPLAY.       11600000
                                                                        11700000
                                                                        11800000
       COPY DFH0XM1.                                                    11900000
       COPY DFH0XM2U.                                                   12000000
                                                                        12100000
       COPY DFHAID.                                                     12200000
                                                                        12300000
      *----------------------------------------------------------------*12400000
                                                                        12500000
      ******************************************************************12600000
      *    L I N K A G E   S E C T I O N                                12700000
      ******************************************************************12800000
       LINKAGE SECTION.                                                 12900000
                                                                        13000000
       01 DFHCOMMAREA.                                                  13100000
           03 LINK-COMMAREA            PIC X(998).                      13200000
           03 WORKING-STORAGE-DATA     PIC X(42).                       13300000
                                                                        13400000
                                                                        13500000
      ******************************************************************13600000
      *    P R O C E D U R E S                                          13700000
      ******************************************************************13800000
       PROCEDURE DIVISION.                                              13900000
                                                                        14000000
      *----------------------------------------------------------------*14100000
       MAINLINE SECTION.                                                14200000
                                                                        14300000
      *----------------------------------------------------------------*14400000
      * Common code                                                    *14500000
      *----------------------------------------------------------------*14600000
      * initialize working storage variables                            14700000
           INITIALIZE ERROR-MSG.                                        14800000
                                                                        14900000
      * set up general variable                                         15000000
           MOVE EIBTRNID TO WS-TRANSID.                                 15100000
           MOVE EIBTRMID TO WS-TERMID.                                  15200000
           MOVE EIBTASKN TO WS-TASKNUM.                                 15300000
                                                                        15400000
           IF EIBCALEN EQUAL ZERO                                       15500000
      *    First invocation                                             15600000
      *    Set up name of inquire queue used to support scrolling       15700000
               STRING WS-TERMID 'EXIQ'                                  15800000
                   DELIMITED BY SIZE                                    15900000
                   INTO WS-INQ-QNAME                                    16000000
               END-STRING                                               16100000
           END-IF                                                       16200000
                                                                        16300000
                                                                        16400000
           IF EIBCALEN > ZERO                                           16500000
               MOVE DFHCOMMAREA TO WS-COMMAREA                          16600000
               MOVE LOW-VALUE TO EXMENUO                                16700000
               MOVE LOW-VALUE TO EXORDRO                                16800000
               MOVE LOW-VALUE TO EXINQCO                                16900000
      *        Read in configuration file and set up program names      17000000
           END-IF                                                       17100000
                                                                        17200000
           EXEC CICS IGNORE CONDITION MAPFAIL END-EXEC                  17300000
                                                                        17400000
      *-----------------------------------------------------------------17500000
      * Check which operation is being requested                        17600000
      *----------------------------------------------------------------*17700000
                                                                        17800000
           EVALUATE TRUE                                                17900000
               WHEN EIBCALEN EQUAL ZERO                                 18000000
      *        First Invocation - set up commarea and send main menu    18100000
      *        Set up switches                                          18200000
                   SET SEND-ERASE TO TRUE                               18300000
                   MOVE LOW-VALUE TO EXMENUO                            18400000
                   PERFORM SEND-MAIN-MENU                               18500000
                                                                        18600000
               WHEN EIBAID EQUAL DFHCLEAR                               18700000
      *        Clear key pressed - clear data on map                    18800000
                   PERFORM CLEAR-MAP                                    18900000
                                                                        19000000
               WHEN EIBAID EQUAL DFHPA1 OR DFHPA2 OR DFHPA3             19100000
      *        Attention keys - do nothing special                      19200000
                                                                        19300000
               WHEN EIBAID EQUAL DFHPF3                                 19400000
      *        Exit application                                         19500000
                   PERFORM APPLICATION-EXIT                             19600000
                                                                        19700000
               WHEN EIBAID EQUAL DFHPF7                                 19800000
      *        Inquiry menu scroll backwards                            19900000
                   IF PROCESSING-INQ-MAP                                20000000
                       PERFORM PROCESS-INQUIRY-MAP-PREVIOUS             20100000
                   ELSE                                                 20200000
                       MOVE 'OPTION NOT RECOGNISED' TO MSG1O            20300000
                       PERFORM INVALID-MENU-INPUT                       20400000
                   END-IF                                               20500000
                                                                        20600000
               WHEN EIBAID EQUAL DFHPF8                                 20700000
      *        Inquiry menu scroll backwards                            20800000
                   IF PROCESSING-INQ-MAP                                20900000
                       PERFORM PROCESS-INQUIRY-MAP-NEXT                 21000000
                   ELSE                                                 21100000
                       MOVE 'OPTION NOT RECOGNISED' TO MSG1O            21200000
                       PERFORM INVALID-MENU-INPUT                       21300000
                   END-IF                                               21400000
                                                                        21500000
               WHEN EIBAID EQUAL DFHPF12                                21600000
      *        Cancel request                                           21700000
      *          If on main menu - exit                                 21800000
                   IF PROCESSING-MENU-MAP                               21900000
                       PERFORM APPLICATION-EXIT                         22000000
      *          Else - send main menu map                              22100000
                   ELSE                                                 22200000
      *                If on inquiry panel delete data used for scrolls 22300000
                       IF PROCESSING-INQ-MAP                            22400000
                           PERFORM DELETE-INQ-Q                         22500000
                       END-IF                                           22600000
                                                                        22700000
                       SET SEND-ERASE TO TRUE                           22800000
                       PERFORM SEND-MAIN-MENU                           22900000
                   END-IF                                               23000000
                                                                        23100000
                                                                        23200000
               WHEN EIBAID EQUAL DFHENTER                               23300000
      *        Process input from map                                   23400000
                   EVALUATE TRUE                                        23500000
                       WHEN PROCESSING-MENU-MAP                         23600000
                           PERFORM PROCESS-MENU-INPUT                   23700000
                       WHEN PROCESSING-INQ-MAP                          23800000
                           PERFORM PROCESS-INQUIRE-INPUT                23900000
                       WHEN PROCESSING-ORDER-MAP                        24000000
                           PERFORM PROCESS-ORDER-INPUT                  24100000
                   END-EVALUATE                                         24200000
                                                                        24300000
               WHEN OTHER                                               24400000
      *        Input not recognised - send error message                24500000
                   MOVE 'OPTION NOT RECOGNISED' TO MSG1O                24600000
                   PERFORM INVALID-MENU-INPUT                           24700000
           END-EVALUATE                                                 24800000
                                                                        24900000
      * Return to caller                                                25000000
           EXEC CICS RETURN TRANSID(WS-TRANSID)                         25100000
                            COMMAREA(WS-COMMAREA)                       25200000
           END-EXEC.                                                    25300000
                                                                        25400000
       MAINLINE-EXIT.                                                   25500000
           EXIT.                                                        25600000
      *----------------------------------------------------------------*25700000
                                                                        25800000
      *================================================================*25900000
      * Procedure to write error message to TD QUEUE(CSMT)             *26000000
      *   message will include Date, Time, Program Name,               *26100000
      *   and error details.                                           *26200000
      *================================================================*26300000
       WRITE-ERROR-MESSAGE.                                             26400000
      * Obtain and format current time and date                         26500000
           EXEC CICS ASKTIME ABSTIME(ABS-TIME)                          26600000
           END-EXEC                                                     26700000
           EXEC CICS FORMATTIME ABSTIME(ABS-TIME)                       26800000
                     MMDDYYYY(DATE1)                                    26900000
                     TIME(TIME1)                                        27000000
           END-EXEC                                                     27100000
           MOVE DATE1 TO EM-DATE                                        27200000
           MOVE TIME1 TO EM-TIME                                        27300000
      * Write output message to TDQ                                     27400000
           EXEC CICS WRITEQ TD QUEUE('CSMT')                            27500000
                     FROM(ERROR-MSG)                                    27600000
                     LENGTH(LENGTH OF ERROR-MSG)                        27700000
           END-EXEC.                                                    27800000
           EXIT.                                                        27900000
                                                                        28000000
                                                                        28100000
      *================================================================*28200000
      * Procedure to send the main menu BMS map                        *28300000
      *================================================================*28400000
        SEND-MAIN-MENU.                                                 28500000
           SET PROCESSING-MENU-MAP TO TRUE                              28600000
           EVALUATE TRUE                                                28700000
               WHEN SEND-ERASE                                          28800000
                   EXEC CICS SEND MAP('EXMENU')                         28900000
                                  MAPSET('DFH0XS1')                     29000000
                                  FROM (EXMENUO)                        29100000
                                  ERASE                                 29200000
                   END-EXEC                                             29300000
               WHEN SEND-DATAONLY                                       29400000
                   EXEC CICS SEND MAP('EXMENU')                         29500000
                                  MAPSET('DFH0XS1')                     29600000
                                  FROM(EXMENUO)                         29700000
                                  DATAONLY                              29800000
                   END-EXEC                                             29900000
               WHEN SEND-ALARM                                          30000000
                   EXEC CICS SEND MAP('EXMENU')                         30100000
                                  MAPSET('DFH0XS1')                     30200000
                                  FROM(EXMENUO)                         30300000
                                  ERASE                                 30400000
                                  ALARM                                 30500000
                   END-EXEC                                             30600000
           END-EVALUATE                                                 30700000
           EXIT.                                                        30800000
                                                                        30900000
      *================================================================*31000000
      * Procedure to send the order panel BMS map                      *31100000
      *================================================================*31200000
        SEND-ORDER-PANEL.                                               31300000
                                                                        31400000
           SET PROCESSING-ORDER-MAP TO TRUE                             31500000
           EVALUATE TRUE                                                31600000
               WHEN SEND-ERASE                                          31700000
                   EXEC CICS SEND MAP('EXORDR')                         31800000
                                  MAPSET('DFH0XS1')                     31900000
                                  FROM (EXORDRO)                        32000000
                                  ERASE                                 32100000
                   END-EXEC                                             32200000
               WHEN SEND-DATAONLY                                       32300000
                   EXEC CICS SEND MAP('EXORDR')                         32400000
                                  MAPSET('DFH0XS1')                     32500000
                                  FROM(EXORDRO)                         32600000
                                  DATAONLY                              32700000
                   END-EXEC                                             32800000
               WHEN SEND-ALARM                                          32900000
                   EXEC CICS SEND MAP('EXORDR')                         33000000
                                  MAPSET('DFH0XS1')                     33100000
                                  FROM(EXORDRO)                         33200000
                                  DATAONLY                              33300000
                                  ALARM                                 33400000
                   END-EXEC                                             33500000
           END-EVALUATE                                                 33600000
           EXIT.                                                        33700000
                                                                        33800000
      *================================================================*33900000
      * Procedure to send the inquire panel BMS map                    *34000000
      *================================================================*34100000
        SEND-INQUIRE-PANEL.                                             34200000
           SET PROCESSING-INQ-MAP TO TRUE                               34300000
           EVALUATE TRUE                                                34400000
               WHEN SEND-ERASE                                          34500000
                   EXEC CICS SEND MAP('EXINQC')                         34600000
                                  MAPSET('DFH0XS2')                     34700000
                                  FROM (EXINQCO)                        34800000
                                  ERASE                                 34900000
                   END-EXEC                                             35000000
               WHEN SEND-DATAONLY                                       35100000
                   EXEC CICS SEND MAP('EXINQC')                         35200000
                                  MAPSET('DFH0XS2')                     35300000
                                  FROM(EXINQCO)                         35400000
                                  DATAONLY                              35500000
                   END-EXEC                                             35600000
               WHEN SEND-ALARM                                          35700000
                   EXEC CICS SEND MAP('EXINQC')                         35800000
                                  MAPSET('DFH0XS2')                     35900000
                                  FROM(EXINQCO)                         36000000
                                  DATAONLY                              36100000
                                  ALARM                                 36200000
                   END-EXEC                                             36300000
           END-EVALUATE                                                 36400000
           EXIT.                                                        36500000
                                                                        36600000
                                                                        36700000
                                                                        36800000
      *================================================================*36900000
      * Procedure to terminate the application and exit                *37000000
      *================================================================*37100000
        APPLICATION-EXIT.                                               37200000
           IF PROCESSING-INQ-MAP                                        37300000
               PERFORM DELETE-INQ-Q                                     37400000
           END-IF                                                       37500000
           EXEC CICS SEND TEXT FROM(PROGRAM-EXIT-MESSAGE)               37600000
                     ERASE                                              37700000
                     FREEKB                                             37800000
           END-EXEC                                                     37900000
           EXEC CICS RETURN END-EXEC                                    38000000
           EXIT.                                                        38100000
                                                                        38200000
      *================================================================*38300000
      * Procedure to clear the user entered data and redisplay map     *38400000
      *================================================================*38500000
        CLEAR-MAP.                                                      38600000
           EVALUATE TRUE                                                38700000
               WHEN PROCESSING-MENU-MAP                                 38800000
                   MOVE LOW-VALUE TO EXMENUO                            38900000
                   SET SEND-ERASE TO TRUE                               39000000
                   PERFORM SEND-MAIN-MENU                               39100000
               WHEN PROCESSING-INQ-MAP                                  39200000
                   MOVE LOW-VALUE TO EXINQCO                            39300000
                   PERFORM POPULATE-INQUIRE-MAP                         39400000
                   SET SEND-ERASE TO TRUE                               39500000
                   PERFORM SEND-INQUIRE-PANEL                           39600000
               WHEN PROCESSING-ORDER-MAP                                39700000
                   MOVE 1 TO WS-LOOP-COUNTER                            39800000
                   MOVE LOW-VALUE TO EXORDRO                            39900000
                   MOVE CA-ITEM-REF(WS-LOOP-COUNTER) TO ORDR-ITEMREFO   40000000
                   MOVE CA-DESCRIPTION(WS-LOOP-COUNTER)                 40100000
                           TO ORDR-DESCO                                40200000
                   MOVE CA-COST(WS-LOOP-COUNTER) TO  ORDR-COSTO         40300000
                   INSPECT ORDR-COSTO REPLACING LEADING '0' BY ' '      40400000
                                                                        40500000
                   MOVE IN-STOCK(WS-LOOP-COUNTER) TO ORDR-STKO          40600000
                   MOVE ON-ORDER(WS-LOOP-COUNTER) TO ORDR-ORDO          40700000
                   SET SEND-ERASE TO TRUE                               40800000
                   PERFORM SEND-ORDER-PANEL                             40900000
           EXIT.                                                        41000000
                                                                        41100000
      *================================================================*41200000
      * Procedure to process the input from the main menu panel        *41300000
      *================================================================*41400000
        PROCESS-MENU-INPUT.                                             41500000
                                                                        41600000
           EXEC CICS RECEIVE MAP('EXMENU')                              41700000
                             MAPSET('DFH0XS1')                          41800000
                             INTO(EXMENUI)                              41900000
           END-EXEC                                                     42000000
                                                                        42100000
           IF ACTIONI EQUAL SPACES                                      42200000
               MOVE 'INVALID OPTION: PLEASE SELECT 1, 2 OR 3' TO MSG1O  42300000
                   PERFORM INVALID-MENU-INPUT                           42400000
           ELSE                                                         42500000
                                                                        42600000
                                                                        42700000
                                                                        42800000
               EVALUATE ACTIONI                                         42900000
                   WHEN '1'                                             43000000
      *            Process inquire of catalog                           43100000
                       PERFORM PROCESS-MENU-INQUIRE-REQUEST             43200000
                   WHEN '2'                                             43300000
      *            Process Order Item                                   43400000
                       PERFORM PROCESS-MENU-ORDER-REQUEST               43500000
                   WHEN '3'                                             43600000
      *            Exit application                                     43700000
                       PERFORM APPLICATION-EXIT                         43800000
                   WHEN OTHER                                           43900000
                       MOVE 'INVALID OPTION: PLEASE SELECT 1, 2 OR 3'   44000000
                               TO MSG1O                                 44100000
                       PERFORM INVALID-MENU-INPUT                       44200000
               END-EVALUATE                                             44300000
           END-IF                                                       44400000
           EXIT.                                                        44500000
      *================================================================*44600000
      * Procedure to process the input from the inquire panel          *44700000
      *================================================================*44800000
        PROCESS-MENU-INQUIRE-REQUEST.                                   44900000
           PERFORM INITIALIZE-LINK-COMMAREA                             45000000
           MOVE 0000 TO WS-INQ-START-ITEM-REF                           45100000
                                                                        45200000
           PERFORM CATALOG-INQUIRE                                      45300000
                                                                        45400000
           EXIT.                                                        45500000
                                                                        45600000
      *================================================================*45700000
      * Procedure to process the input from the order panel            *45800000
      *================================================================*45900000
        PROCESS-MENU-ORDER-REQUEST.                                     46000000
           INSPECT ITEM-REFI REPLACING LEADING ' ' BY ZERO              46100000
                                                                        46200000
           EVALUATE TRUE                                                46300000
               WHEN ITEM-REFI EQUAL LOW-VALUE OR SPACE                  46400000
                   MOVE 'TO PLACE ORDER PLEASE ENTER A VALID ITEM REF'  46500000
                       TO MSG1O                                         46600000
                   PERFORM INVALID-MENU-INPUT                           46700000
               WHEN ITEM-REFI NOT NUMERIC                               46800000
                   MOVE 'TO PLACE ORDER PLEASE ENTER A VALID ITEM REF'  46900000
                       TO MSG1O                                         47000000
                   PERFORM INVALID-MENU-INPUT                           47100000
               WHEN OTHER                                               47200000
                   PERFORM INITIALIZE-LINK-COMMAREA                     47300000
                   MOVE '01INQS' TO CA-REQUEST-ID                       47400000
                   MOVE ITEM-REFI TO CA-ITEM-REF-REQ                    47500000
                   EXEC CICS LINK PROGRAM(WS-WEBSERVICE-CLIENT-PROG)    47600000
                                  COMMAREA(WS-COMMAREA)                 47700000
                                  DATALENGTH(LINK-COMMAREA-LENGTH)      47800000
                   END-EXEC                                             47900000
                                                                        48000000
               IF CA-RETURN-CODE EQUAL '00'                             48100000
                       MOVE LOW-VALUE TO EXORDRO                        48200000
                       MOVE ITEM-REFI TO ORDR-ITEMREFO                  48300000
                       MOVE CA-SNGL-DESCRIPTION TO ORDR-DESCO           48400000
                                                                        48500000
                       MOVE CA-SNGL-COST TO  ORDR-COSTO                 48600000
                       INSPECT ORDR-COSTO REPLACING LEADING '0' BY ' '  48700000
                                                                        48800000
                       MOVE IN-SNGL-STOCK TO ORDR-STKO                  48900000
                       MOVE ON-SNGL-ORDER TO ORDR-ORDO                  49000000
                                                                        49100000
                       MOVE ITEM-REFI TO WS-ORDER-ITEM-REF              49200000
                                                                        49300000
                       SET SEND-ERASE TO TRUE                           49400000
                       PERFORM SEND-ORDER-PANEL                         49500000
                   ELSE                                                 49600000
                       MOVE CA-RESPONSE-MESSAGE TO MSG1O                49700000
                       PERFORM INVALID-MENU-INPUT                       49800000
                   END-IF                                               49900000
           END-EVALUATE                                                 50000000
                                                                        50100000
                                                                        50200000
                                                                        50300000
                                                                        50400000
           EXIT.                                                        50500000
      *================================================================*50600000
      * Procedure to handle unknown input on the main menu             *50700000
      *================================================================*50800000
        INVALID-MENU-INPUT.                                             50900000
           SET SEND-ALARM TO TRUE                                       51000000
           PERFORM SEND-MAIN-MENU                                       51100000
           EXIT.                                                        51200000
                                                                        51300000
      *================================================================*51400000
      * Procedure to handle errors from the order panel                *51500000
      *================================================================*51600000
        ORDER-ERROR.                                                    51700000
                                                                        51800000
           SET SEND-ALARM TO TRUE                                       51900000
           PERFORM SEND-ORDER-PANEL                                     52000000
           EXIT.                                                        52100000
                                                                        52200000
      *================================================================*52300000
      * Procedure to link to Datastore program to inquire              *52400000
      *   on the catalog data                                          *52500000
      *================================================================*52600000
        CATALOG-INQUIRE.                                                52700000
                                                                        52800000
           MOVE '01INQC' TO CA-REQUEST-ID                               52900000
           MOVE WS-INQ-START-ITEM-REF TO CA-LIST-START-REF              53000000
                                                                        53100000
           EXEC CICS LINK PROGRAM(WS-WEBSERVICE-CLIENT-PROG)            53200000
                          COMMAREA(WS-COMMAREA)                         53300000
                          DATALENGTH(LINK-COMMAREA-LENGTH)              53400000
           END-EXEC                                                     53500000
                                                                        53600000
           IF CA-RETURN-CODE EQUAL 00                                   53700000
                                                                        53800000
               MOVE LOW-VALUE TO EXINQCO                                53900000
               PERFORM POPULATE-INQUIRE-MAP                             54000000
                                                                        54100000
               SET SEND-ERASE TO TRUE                                   54200000
               PERFORM SEND-INQUIRE-PANEL                               54300000
           ELSE                                                         54400000
               MOVE CA-RESPONSE-MESSAGE TO MSG1O                        54500000
               PERFORM INVALID-MENU-INPUT                               54600000
           END-IF                                                       54700000
                                                                        54800000
           EXIT.                                                        54900000
                                                                        55000000
      *================================================================*55100000
      * Procedure to process the input from the inquire panel          *55200000
      *================================================================*55300000
        PROCESS-INQUIRE-INPUT.                                          55400000
      *    Receive the map                                              55500000
           EXEC CICS RECEIVE MAP('EXINQC')                              55600000
                             MAPSET('DFH0XS2')                          55700000
                             INTO(EXINQCI)                              55800000
           END-EXEC                                                     55900000
                                                                        56000000
      *    Check to see if any of the select markers have been checked  56100000
           SET ORDER-REQ-NOT-FOUND TO TRUE                              56200000
           SET DATA-VALID TO TRUE                                       56300000
           PERFORM                                                      56400000
           WITH TEST AFTER                                              56500000
           VARYING WS-LOOP-COUNTER FROM 1 BY 1                          56600000
           UNTIL ORDER-REQ-FOUND OR                                     56700000
                 WS-LOOP-COUNTER EQUAL 15                               56800000
                                                                        56900000
               IF INQ-ORDI(WS-LOOP-COUNTER) EQUAL '/'                   57000000
      *        Item selected - Inquire again to check details           57100000
                   SET ORDER-REQ-FOUND TO TRUE                          57200000
                                                                        57300000
      *            Check to make sure there is an item for this select  57400000
                   IF CA-ITEM-REF(WS-LOOP-COUNTER) NOT NUMERIC          57500000
                       SET DATA-INVALID TO TRUE                         57600000
                   ELSE                                                 57700000
                       MOVE CA-ITEM-REF(WS-LOOP-COUNTER)                57800000
                                   TO WS-ORDER-ITEM-REF                 57900000
                                                                        58000000
                       PERFORM INITIALIZE-LINK-COMMAREA                 58100000
                       MOVE '01INQS' TO CA-REQUEST-ID                   58200000
                       MOVE WS-ORDER-ITEM-REF TO CA-ITEM-REF-REQ        58300000
                       EXEC CICS LINK PROGRAM(WS-WEBSERVICE-CLIENT-PROG)58400000
                                      COMMAREA(WS-COMMAREA)             58500000
                                      DATALENGTH(LINK-COMMAREA-LENGTH)  58600000
                       END-EXEC                                         58700000
                                                                        58800000
                       IF CA-RETURN-CODE EQUAL '00'                     58900000
                           MOVE LOW-VALUE TO EXORDRO                    59000000
                           MOVE CA-SNGL-ITEM-REF TO ORDR-ITEMREFO       59100000
                           MOVE CA-SNGL-DESCRIPTION TO ORDR-DESCO       59200000
                           MOVE CA-SNGL-COST TO  ORDR-COSTO             59300000
                           INSPECT ORDR-COSTO                           59400000
                               REPLACING LEADING '0' BY ' '             59500000
                           MOVE IN-SNGL-STOCK TO ORDR-STKO              59600000
                           MOVE ON-SNGL-ORDER TO ORDR-ORDO              59700000
                                                                        59800000
      *                    Clean up data used for scrolling inquire     59900000
                           PERFORM DELETE-INQ-Q                         60000000
                                                                        60100000
                           SET SEND-ERASE TO TRUE                       60200000
                           PERFORM SEND-ORDER-PANEL                     60300000
                       ELSE                                             60400000
                           MOVE CA-RESPONSE-MESSAGE TO MSG1O            60500000
                           PERFORM INVALID-MENU-INPUT                   60600000
                       END-IF                                           60700000
                   END-IF                                               60800000
               END-IF                                                   60900000
           END-PERFORM                                                  61000000
                                                                        61100000
           IF ORDER-REQ-NOT-FOUND OR DATA-INVALID                       61200000
               MOVE LOW-VALUE TO EXINQCO                                61400000
               PERFORM POPULATE-INQUIRE-MAP                             61600000
               MOVE 'PLEASE SELECT AN ITEM TO ORDER WITH A /'           61800000
                    TO INQC-MSGO                                        62000000
               SET SEND-ERASE TO TRUE                                   62200000
               PERFORM SEND-INQUIRE-PANEL                               62400000
           END-IF                                                       62600000
                                                                        62800000
                                                                        63000000
           EXIT.                                                        63200000
      *================================================================*63400000
      * Procedure to populate the map data from the commarea           *63600000
      *================================================================*63800000
        POPULATE-INQUIRE-MAP.                                           64000000
           PERFORM                                                      64200000
           WITH TEST AFTER                                              64400000
           VARYING  WS-LOOP-COUNTER FROM 1 BY 1                         64600000
           UNTIL WS-LOOP-COUNTER EQUAL CA-ITEM-COUNT                    64800000
              MOVE CA-ITEM-REF(WS-LOOP-COUNTER)                         65000000
                    TO INQ-ITEMREFO(WS-LOOP-COUNTER)                    65200000
              MOVE CA-DESCRIPTION(WS-LOOP-COUNTER)                      65400000
                    TO INQ-DESCO(WS-LOOP-COUNTER)                       65600000
                                                                        65800000
              MOVE CA-COST(WS-LOOP-COUNTER)                             66000000
                    TO INQ-COSTO(WS-LOOP-COUNTER)                       66200000
                                                                        66400000
              INSPECT INQ-COSTO(WS-LOOP-COUNTER)                        66600000
                    REPLACING LEADING '0' BY ' '                        66800000
                                                                        67000000
           END-PERFORM                                                  67200000
           EXIT.                                                        67400000
                                                                        67600000
                                                                        67800000
      *================================================================*68000000
      * Procedure to process the input from the order panel            *68200000
      *================================================================*68400000
        PROCESS-ORDER-INPUT.                                            68600000
           EXEC CICS RECEIVE MAP('EXORDR')                              68800000
                             MAPSET('DFH0XS1')                          69000000
                             INTO(EXORDRI)                              69200000
           END-EXEC                                                     69400000
                                                                        69600000
      * Check input data is valid                                       69800000
                                                                        70000000
           SET DATA-VALID TO TRUE                                       70200000
                                                                        70400000
           INSPECT ORDR-QUANTITYI REPLACING LEADING ' ' BY ZERO         70600000
                                                                        70800000
           IF ORDR-QUANTITYI NOT NUMERIC                                71000000
            OR ORDR-QUANTITYI NOT GREATER THAN ZERO                     71200000
               MOVE 'TO PLACE ORDER PLEASE ENTER A VALID QUANTITY'      71400000
                   TO ORDR-MSGO                                         71600000
               SET DATA-INVALID TO TRUE                                 71800000
           END-IF                                                       72000000
                                                                        72200000
           IF ORDR-USERIDL EQUAL ZERO                                   72400000
               OR ORDR-DEPTL EQUAL ZERO                                 72600000
               OR ORDR-USERIDI EQUAL SPACE                              72800000
               OR ORDR-DEPTI EQUAL SPACE                                73000000
               MOVE 'TO PLACE ORDER PLEASE ENTER A VALID USERID AND DEPA73200000
      -             'RTMENT'                                            73400000
                   TO ORDR-MSGO                                         73600000
               SET DATA-INVALID TO TRUE                                 73800000
           END-IF                                                       74000000
                                                                        74200000
                                                                        74400000
           IF DATA-VALID                                                74600000
               PERFORM INITIALIZE-LINK-COMMAREA                         74800000
                                                                        75000000
               MOVE '01ORDR' TO CA-REQUEST-ID                           75200000
                                                                        75400000
               MOVE ORDR-USERIDI TO CA-USERID                           75600000
               MOVE ORDR-DEPTI TO CA-CHARGE-DEPT                        75800000
               MOVE WS-ORDER-ITEM-REF TO CA-ITEM-REF-NUMBER             76000000
               MOVE ORDR-QUANTITYI TO CA-QUANTITY-REQ                   76200000
                                                                        76400000
               EXEC CICS LINK PROGRAM(WS-WEBSERVICE-CLIENT-PROG)        76600000
                              COMMAREA(WS-COMMAREA)                     76800000
                              DATALENGTH(LINK-COMMAREA-LENGTH)          77000000
               END-EXEC                                                 77200000
                                                                        77400000
               IF CA-RETURN-CODE EQUAL '97'                             77600000
      *        Insufficient stock to complete order                     77800000
                   MOVE 'INSUFFICIENT STOCK TO COMPLETE ORDER'          78000000
                       TO ORDR-MSGO                                     78200000
                   PERFORM ORDER-ERROR                                  78400000
               ELSE                                                     78600000
                   MOVE CA-RESPONSE-MESSAGE TO MSG1O                    78800000
                   SET SEND-ERASE TO TRUE                               79000000
                   PERFORM SEND-MAIN-MENU                               79200000
               END-IF                                                   79400000
           ELSE                                                         79600000
               PERFORM ORDER-ERROR                                      79800000
           END-IF                                                       80000000
                                                                        80200000
           EXIT.                                                        80400000
                                                                        80600000
      *================================================================*80800000
      * Procedure to initialize the commarea prior to requests         *81000000
      *================================================================*81200000
        INITIALIZE-LINK-COMMAREA.                                       81400000
           MOVE LOW-VALUE TO CA-REQUEST-ID                              81600000
           MOVE 00 TO CA-RETURN-CODE                                    81800000
           MOVE LOW-VALUE TO CA-RESPONSE-MESSAGE                        82000000
           INITIALIZE CA-REQUEST-SPECIFIC                               82200000
           EXIT.                                                        82400000
                                                                        82600000
      *================================================================*82800000
      * Procedure to scroll forwards on the inquire map                *83000000
      *================================================================*83200000
        PROCESS-INQUIRY-MAP-PREVIOUS.                                   83400000
           IF WS-INQ-ITEM-LIST-CURRENT EQUAL 1                          83600000
      *        We are on the first panel so cannot scroll back          83800000
               PERFORM POPULATE-INQUIRE-MAP                             84000000
               MOVE 'START OF DATA' TO INQC-MSGO                        84200000
               SET SEND-ERASE TO TRUE                                   84400000
               PERFORM SEND-INQUIRE-PANEL                               84600000
           ELSE                                                         84800000
      *        Scroll back to previous panel                            85000000
               SUBTRACT 1 FROM WS-INQ-ITEM-LIST-CURRENT                 85200000
               EXEC CICS READQ TS QUEUE(WS-INQ-QNAME)                   85400000
                                  ITEM(WS-INQ-ITEM-LIST-CURRENT)        85600000
                                  INTO(WS-INQ-START-ITEM-REF)           85800000
               END-EXEC                                                 86000000
                                                                        86200000
               PERFORM CATALOG-INQUIRE                                  86400000
                                                                        86600000
                                                                        86800000
           END-IF                                                       87000000
                                                                        87200000
           EXIT.                                                        87400000
                                                                        87600000
      *================================================================*87800000
      * Procedure to scroll forwards on the inquire map                *88000000
      *================================================================*88200000
        PROCESS-INQUIRY-MAP-NEXT.                                       88400000
           IF DATA-NOT-SCROLLED                                         88600000
      *    First time scrolling - set flag and inital value in list     88800000
               SET DATA-SCROLLED TO TRUE                                89000000
               EXEC CICS WRITEQ TS QUEUE(WS-INQ-QNAME)                  89200000
                                FROM(WS-INQ-START-ITEM-REF)             89400000
               END-EXEC                                                 89600000
           END-IF                                                       89800000
                                                                        90000000
           IF CA-ITEM-COUNT LESS THAN 15                                90200000
               PERFORM POPULATE-INQUIRE-MAP                             90400000
               MOVE 'END OF DATA' TO INQC-MSGO                          90600000
               SET SEND-ERASE TO TRUE                                   90800000
               PERFORM SEND-INQUIRE-PANEL                               91000000
           ELSE                                                         91200000
               IF WS-INQ-ITEM-LIST-CURRENT EQUAL WS-INQ-ITEM-LIST-DEPTH 91400000
                   MOVE CA-LAST-ITEM-REF TO WS-INQ-START-ITEM-REF       91600000
      *            Add current first item in list to the inq queue      91800000
                   EXEC CICS WRITEQ TS QUEUE(WS-INQ-QNAME)              92000000
                                    FROM(WS-INQ-START-ITEM-REF)         92200000
                   END-EXEC                                             92400000
                   ADD 1 TO WS-INQ-ITEM-LIST-DEPTH                      92600000
                   ADD 1 TO WS-INQ-ITEM-LIST-CURRENT                    92800000
               ELSE                                                     93000000
                   ADD 1 TO WS-INQ-ITEM-LIST-CURRENT                    93200000
      *            Read the item-ref to start inquire from the ts queue 93400000
                   EXEC CICS READQ TS QUEUE(WS-INQ-QNAME)               93600000
                                      ITEM(WS-INQ-ITEM-LIST-CURRENT)    93800000
                                      INTO(WS-INQ-START-ITEM-REF)       94000000
                   END-EXEC                                             94200000
               END-IF                                                   94400000
               PERFORM CATALOG-INQUIRE                                  94600000
           END-IF                                                       94800000
                                                                        95000000
           EXIT.                                                        95200000
                                                                        95400000
      *================================================================*95600000
      * Procedure to delete the TS queue of inquiry item refs          *95800000
      *================================================================*96000000
        DELETE-INQ-Q.                                                   96200000
           IF DATA-SCROLLED                                             96400000
                SET DATA-NOT-SCROLLED TO TRUE                           96600000
                MOVE 1 TO WS-INQ-ITEM-LIST-CURRENT                      96800000
                MOVE 1 TO WS-INQ-ITEM-LIST-DEPTH                        97000000
                                                                        97200000
                EXEC CICS DELETEQ TS QUEUE(WS-INQ-QNAME)                97400000
                END-EXEC                                                97600000
           END-IF                                                       97800000
           EXIT.                                                        98000000
                                                                        98200000
        DEBUG-OUT.                                                      98400000
           IF DEBUG-ON                                                  98600000
               EXEC CICS WRITEQ TS  QUEUE('DEBUG-Q')                    98800000
                                    FROM (DEBUG-STRING)                 99000000
               END-EXEC                                                 99200000
           END-IF                                                       99400000
           EXIT.                                                        99600000
      *---------------------------------------------------------------* 99800000
