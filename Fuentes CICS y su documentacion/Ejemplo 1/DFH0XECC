             CBL CICS('COBOL3') APOST                                   00100000
      ***************************************************************** 00200000
      *                                                               * 00300000
      *  MODULE NAME = DFH0XECC                                       * 00400000
      *                                                               * 00500000
      *  DESCRIPTIVE NAME = CICS     (Samples) Example Application -  * 00600000
      *                     Web Service Client INVOKE WEBSERVICE      * 00700000
      *                                                               * 00800000
      *  @BANNER_START                           01                   * 00916600
      *  Licensed Materials - Property of IBM                         * 01033200
      *                                                               * 01149800
      *  5655-M15              DFH0XECC                               * 01266400
      *                                                               * 01383000
      *  (C) Copyright IBM Corp. 2005, 2006                           * 01499600
      *                                                               * 01616200
      *  CICS                                                         * 01732800
      *  (Element of CICS Transaction Server                          * 01849400
      *  for z/OS, Version 3 Release 2)                               * 01966000
      *  @BANNER_END                                                  * 02082600
      *                                                               * 02200000
      * STATUS = 6.5.0                                                * 02300000
      *                                                               * 02400000
      *  TRANSACTION NAME = n/a                                       * 02500000
      *                                                               * 02600000
      *  FUNCTION =                                                   * 02700000
      *  This module contains the logic to handle the clients INVOKE  * 02800000
      *  WEBSERVICE calls to the example application                  * 02900000
      *                                                               * 03000000
      *-------------------------------------------------------------  * 03100000
      *                                                               * 03200000
      *  ENTRY POINT = DFH0XECC                                       * 03300000
      *                                                               * 03400000
      *-------------------------------------------------------------  * 03500000
      *                                                               * 03600000
      *  CHANGE ACTIVITY :                                            * 03700000
      *                                                               * 03800000
      *      $MOD(DFH0XECC),COMP(STARTER),PROD(CICS    ):             * 03900000
      *                                                               * 04000000
      *   PN= REASON REL YYMMDD HDXXIII : REMARKS                     * 04100000
      *  $D0= I07544 640 050113 HDIPCB  : ExampleApp CICS client code * 04200000
      *  $D1= I07544 640 050118 HDIPCB  : ExampleApp Add sample JCL an* 04300000
      *  $P1= D16115 650 061207 HDHCPC  : Error messages for sample ap* 04400000
      *                                                               * 04500000
      ***************************************************************** 04600000
       IDENTIFICATION DIVISION.                                         04700000
       PROGRAM-ID. DFH0XECC.                                            04800000
       ENVIRONMENT DIVISION.                                            04900000
       CONFIGURATION SECTION.                                           05000000
       DATA DIVISION.                                                   05100000
       WORKING-STORAGE SECTION.                                         05200000
      *----------------------------------------------------------------*05300000
      * Common defintions                                              *05400000
      *----------------------------------------------------------------*05500000
      * Run time (debug) infomation for this invocation                 05600000
        01  WS-HEADER.                                                  05700000
           03 WS-EYECATCHER            PIC X(16)                        05800000
                                        VALUE 'DFH0XECC------WS'.       05900000
           03 WS-TRANSID               PIC X(4).                        06000000
           03 WS-TERMID                PIC X(4).                        06100000
           03 WS-TASKNUM               PIC 9(7).                        06200000
           03 WS-CALEN                 PIC S9(4) COMP.                  06300000
                                                                        06400000
      * Variables for time/date processing                              06500000
       01  ABS-TIME                    PIC S9(8) COMP VALUE +0.         06600000
       01  TIME1                       PIC X(8)  VALUE SPACES.          06700000
       01  DATE1                       PIC X(10) VALUE SPACES.          06800000
                                                                        06900000
      * Error Message structure                                         07000000
       01  ERROR-MSG.                                                   07100000
           03 EM-DATE                  PIC X(8)  VALUE SPACES.          07200000
           03 FILLER                   PIC X     VALUE SPACES.          07300000
           03 EM-TIME                  PIC X(6)  VALUE SPACES.          07400000
           03 FILLER                   PIC X(9)  VALUE ' DFH0XECC'.     07500000
           03 FILLER                   PIC X     VALUE SPACES.          07600000
           03 EM-DETAIL                PIC X(50) VALUE SPACES.          07700000
                                                                        07800000
      * Working variables                                               07900000
       01 WORKING-VARIABLES.                                            08000000
           03 WS-REQUEST-ID            PIC X(6).                        08100000
           03 WS-RESP                  PIC S9(8) COMP.                  08200000
           03 WS-RESP2                 PIC S9(8) COMP.                  08300000
           03 WS-OPERATION             PIC X(255).                      08400000
           03 WS-SERVICE-CONT-NAME     PIC X(16).                       08500000
           03 WS-CHANNELNAME           PIC X(16).                       08600000
           03 WS-ENDPOINT-URI          PIC X(255).                      08700000
                                                                        08800000
      * Data area for webservice request                                08900000
       01 INQUIRE-CATALOG-REQUEST.                                      09000000
           COPY DFH0XCPA.                                               09100000
       01 INQUIRE-CATALOG-RESPONSE.                                     09200000
           COPY DFH0XCPB.                                               09300000
       01 INQUIRE-SINGLE-REQUEST.                                       09400000
           COPY DFH0XCPC.                                               09500000
       01 INQUIRE-SINGLE-RESPONSE.                                      09600000
           COPY DFH0XCPD.                                               09700000
       01 PLACE-ORDER-REQUEST.                                          09800000
           COPY DFH0XCPE.                                               09900000
       01 PLACE-ORDER-RESPONSE.                                         10000000
           COPY DFH0XCPF.                                               10100000
                                                                        10200000
      * Key into the configuration file                                 10300000
       01 EXAMPLE-APP-CONFIG       PIC X(9)                             10400000
               VALUE 'WS-SERVER'.                                       10500000
                                                                        10600000
      * Format of the configuration file                                10700000
       01 APP-CONFIG.                                                   10800000
           03 FILE-KEY             PIC X(9).                            10900000
           03 FILLER               PIC X.                               11000000
           03 SERVER-LOCATION      PIC X(200).                          11100000
                                                                        11200000
      *----------------------------------------------------------------*11300000
                                                                        11400000
      ******************************************************************11500000
      *    L I N K A G E   S E C T I O N                                11600000
      ******************************************************************11700000
       LINKAGE SECTION.                                                 11800000
       01 DFHCOMMAREA.                                                  11900000
           COPY DFH0XCP1.                                               12000000
                                                                        12100000
      ******************************************************************12200000
      *    P R O C E D U R E S                                          12400000
      ******************************************************************12600000
       PROCEDURE DIVISION.                                              12800000
                                                                        13000000
      *----------------------------------------------------------------*13200000
       MAINLINE SECTION.                                                13400000
                                                                        13600000
      *----------------------------------------------------------------*13800000
      * Common code                                                    *14000000
      *----------------------------------------------------------------*14200000
      * initialize working storage variables                            14400000
           INITIALIZE APP-CONFIG                                        14600000
           INITIALIZE ERROR-MSG                                         14800000
           INITIALIZE INQUIRE-CATALOG-REQUEST                           15000000
           INITIALIZE INQUIRE-CATALOG-RESPONSE                          15200000
           INITIALIZE INQUIRE-SINGLE-REQUEST                            15400000
           INITIALIZE INQUIRE-SINGLE-RESPONSE                           15600000
           INITIALIZE PLACE-ORDER-REQUEST                               15800000
           INITIALIZE PLACE-ORDER-RESPONSE                              16000000
           INITIALIZE WORKING-VARIABLES                                 16200000
                                                                        16400000
      * set up general variable                                         16600000
           MOVE EIBTRNID TO WS-TRANSID                                  16800000
           MOVE EIBTRMID TO WS-TERMID                                   17000000
           MOVE EIBTASKN TO WS-TASKNUM                                  17200000
                                                                        17400000
      *---------------------------------------------------------------* 17600000
      * Check commarea and obtain required details                    * 17800000
      *---------------------------------------------------------------* 18000000
      * If NO commarea received issue an ABEND                          18200000
           IF EIBCALEN IS EQUAL TO ZERO                                 18400000
               MOVE ' NO COMMAREA RECEIVED' TO EM-DETAIL                18600000
               PERFORM WRITE-ERROR-MESSAGE                              18800000
               EXEC CICS ABEND ABCODE('EXCA') NODUMP END-EXEC           19000000
           END-IF                                                       19200000
                                                                        19400000
      * Initalize commarea return code to zero                          19600000
           MOVE '00' TO CA-RETURN-CODE IN DFHCOMMAREA                   19800000
           MOVE EIBCALEN TO WS-CALEN.                                   20000000
                                                                        20200000
      *----------------------------------------------------------------*20400000
      * Read in configuration file and set server address               20600000
      *----------------------------------------------------------------*20800000
           EXEC CICS READ FILE('EXMPCONF')                              21000000
                          INTO(APP-CONFIG)                              21200000
                          RIDFLD(EXAMPLE-APP-CONFIG)                    21400000
                          RESP(WS-RESP)                                 21600000
           END-EXEC                                                     21800000
                                                                        22000000
           IF WS-RESP NOT EQUAL DFHRESP(NORMAL)                         22200000
               MOVE '51' TO CA-RETURN-CODE IN DFHCOMMAREA               22400000
               MOVE 'APPLICATION ERROR OPENING CONFIGURATION FILE'      22600000
                   TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA                22800000
               EXEC CICS RETURN END-EXEC                                23000000
           END-IF                                                       23200000
                                                                        23400000
      *---------------------------------------------------------------* 23600000
      * Set up the data for the web service call                        23800000
      *---------------------------------------------------------------* 24000000
                                                                        24200000
      *    'DFHWS-DATA' is the name of the container that will store    24400000
      *    the data to make the webservice request                      24600000
           MOVE 'DFHWS-DATA' TO WS-SERVICE-CONT-NAME                    24800000
                                                                        25000000
      *    'SERVICE-CHANNEL' is the name of the channel we will pass to 25200000
      *    the web service call                                         25400000
           MOVE 'SERVICE-CHANNEL' TO WS-CHANNELNAME                     25600000
                                                                        25800000
      *----------------------------------------------------------------*26000000
      * Check which operation in being requested                        26200000
      *----------------------------------------------------------------*26400000
      * Uppercase the value passed in the Request Id field              26600000
           MOVE FUNCTION UPPER-CASE(CA-REQUEST-ID IN DFHCOMMAREA)       26800000
                           TO WS-REQUEST-ID                             27000000
                                                                        27200000
           EVALUATE WS-REQUEST-ID                                       27400000
               WHEN '01INQC'                                            27600000
      *        Call routine to perform for inquire                      27800000
                   PERFORM CATALOG-INQUIRE                              28000000
                                                                        28200000
               WHEN '01INQS'                                            28400000
      *        Call routine to perform for inquire for single item      28600000
                   PERFORM SINGLE-INQUIRE                               28800000
                                                                        29000000
               WHEN '01ORDR'                                            29200000
      *        Call routine to place order                              29400000
                   PERFORM PLACE-ORDER                                  29600000
                                                                        29800000
               WHEN OTHER                                               30000000
      *        Request is not recognised or supported                   30200000
                   PERFORM REQUEST-NOT-RECOGNISED                       30400000
                                                                        30600000
           END-EVALUATE                                                 30800000
                                                                        31000000
      * Return to caller                                                31200000
           EXEC CICS RETURN END-EXEC.                                   31400000
                                                                        31600000
       MAINLINE-EXIT.                                                   31800000
           EXIT.                                                        32000000
      *----------------------------------------------------------------*32200000
                                                                        32400000
      *================================================================*32600000
      * Procedure to write error message to TD QUEUE(CSMT)             *32800000
      *   message will include Date, Time, Program Name,               *33000000
      *   and error details.                                           *33200000
      *================================================================*33400000
       WRITE-ERROR-MESSAGE.                                             33600000
      * Obtain and format current time and date                         33800000
           EXEC CICS ASKTIME ABSTIME(ABS-TIME)                          34000000
           END-EXEC                                                     34200000
           EXEC CICS FORMATTIME ABSTIME(ABS-TIME)                       34400000
                     MMDDYYYY(DATE1)                                    34600000
                     TIME(TIME1)                                        34800000
           END-EXEC                                                     35000000
           MOVE DATE1 TO EM-DATE                                        35200000
           MOVE TIME1 TO EM-TIME                                        35400000
      * Write output message to TDQ                                     35600000
           EXEC CICS WRITEQ TD QUEUE('CSMT')                            35800000
                     FROM(ERROR-MSG)                                    36000000
                     LENGTH(LENGTH OF ERROR-MSG)                        36200000
           END-EXEC.                                                    36400000
           EXIT.                                                        36600000
                                                                        36800000
      *================================================================*37000000
      * Procedure tomake a Web service call to inquire                 *37200000
      *   on the catalog data                                          *37400000
      *================================================================*37600000
        CATALOG-INQUIRE.                                                37800000
                                                                        38000000
      *    Move the data from the input commarea to the                 38200000
      *    'inquireCatalogRequest' variable ready to put in a container 38400000
           MOVE CA-REQUEST-ID IN DFHCOMMAREA                            38600000
               TO caXrequestXid IN INQUIRE-CATALOG-REQUEST              38800000
           MOVE 00 TO caXreturnXcode IN INQUIRE-CATALOG-REQUEST         39000000
           MOVE CA-LIST-START-REF IN DFHCOMMAREA                        39200000
               TO caXlistXstartXref IN INQUIRE-CATALOG-REQUEST          39400000
                                                                        39600000
      *---------------------------------------------------------------* 39800000
      * Place the request data into a container on a channel            40000000
      * WS-SERVICE-CONT-NAME   =   'DFHWS-DATA'                         40200000
      * WS-CHANNELNAME         =   'SERVICE-CHANNEL'                    40400000
      *---------------------------------------------------------------* 40600000
           EXEC CICS PUT CONTAINER(WS-SERVICE-CONT-NAME)                40800000
                         CHANNEL(WS-CHANNELNAME)                        41000000
                         FROM(INQUIRE-CATALOG-REQUEST)                  41200000
           END-EXEC                                                     41400000
                                                                        41600000
      *---------------------------------------------------------------* 41800000
      * Build the URI from the server name in the VSAM file and         42000000
      * the local URI for the inquire catalog service                   42200000
      *---------------------------------------------------------------* 42400000
                                                                        42600000
           STRING  'http://'                                            42800000
                   SERVER-LOCATION                                      43000000
                   '/exampleApp/inquireCatalog'                         43200000
               DELIMITED BY SPACE                                       43400000
               INTO WS-ENDPOINT-URI                                     43600000
           END-STRING                                                   43800000
                                                                        44000000
      *---------------------------------------------------------------* 44200000
      * Invoke the webservice                                           44400000
      * WS-CHANNELNAME         =   'SERVICE-CHANNEL'                    44600000
      * Operation below changed by PK60058                            * 44700000
      *---------------------------------------------------------------* 44800000
           MOVE 'DFH0XCMNOperation' TO WS-OPERATION                     45000000
                                                                        45200000
           EXEC CICS INVOKE WEBSERVICE('inquireCatalogClient')          45400000
                     CHANNEL(WS-CHANNELNAME)                            45600000
                     URI(WS-ENDPOINT-URI)                               45800000
                     OPERATION(WS-OPERATION)                            46000000
                     RESP(WS-RESP) RESP2(WS-RESP2)                      46200000
           END-EXEC.                                                    46400000
                                                                        46600000
      * Check the return code was normal                                46800000
                                                                        47000000
           EVALUATE WS-RESP                                             47200000
               WHEN DFHRESP(NORMAL)                                     47400000
      *            Normal response populate commarea and return         47600000
                                                                        47800000
      *            Get the response container off the channel           48000000
                   EXEC CICS GET CONTAINER(WS-SERVICE-CONT-NAME)        48200000
                             CHANNEL(WS-CHANNELNAME)                    48400000
                             INTO(INQUIRE-CATALOG-RESPONSE)             48600000
                   END-EXEC                                             48800000
                                                                        49000000
                   MOVE caXreturnXcode IN INQUIRE-CATALOG-RESPONSE      49200000
                        TO CA-RETURN-CODE IN DFHCOMMAREA                49400000
                                                                        49600000
                   MOVE caXresponseXmessage IN INQUIRE-CATALOG-RESPONSE 49800000
                        TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA           50000000
                                                                        50200000
      *            Check the return code                                50400000
                   IF caXreturnXcode IN INQUIRE-CATALOG-RESPONSE        50600000
                     EQUAL 00                                           50800000
      *                Response is okay, copy data across to commarea   51000000
                       MOVE caXinquireXrequest                          51200000
                               IN INQUIRE-CATALOG-RESPONSE              51400000
                       TO   CA-INQUIRE-REQUEST IN DFHCOMMAREA           51600000
                   END-IF                                               51800000
                                                                        52000000
               WHEN DFHRESP(INVREQ)                                     52200000
                   MOVE                                                 52400000
                   'Error calling inquire catalog service - INVREQ'     52600000
                       TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA            52800000
                   MOVE 30 TO CA-RETURN-CODE IN DFHCOMMAREA             53000000
                                                                        53200000
               WHEN DFHRESP(NOTFND)                                     53400000
                   MOVE                                                 53600000
                   'Error calling inquire catalog service - NOT FOUND'  53800000
                       TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA            54000000
                   MOVE 31 TO CA-RETURN-CODE IN DFHCOMMAREA             54200000
                                                                        54400000
               WHEN OTHER                                               54600000
                   MOVE                                                 54800000
                   'Error calling inquire catalog service'              55000000
                       TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA            55200000
                   MOVE 32 TO CA-RETURN-CODE IN DFHCOMMAREA             55400000
           END-EVALUATE.                                                55600000
                                                                        55800000
           EXIT.                                                        56000000
                                                                        56200000
      *================================================================*56400000
      * Procedure tomake a Web service call to inquire                 *56600000
      *   on the catalog data                                          *56800000
      *================================================================*57000000
        SINGLE-INQUIRE.                                                 57200000
                                                                        57400000
      *    Move the data from the input commarea to the                 57600000
      *    'inquireSingleRequest' variable ready to put in a container  57800000
           MOVE CA-REQUEST-ID IN DFHCOMMAREA                            58000000
               TO caXrequestXid IN INQUIRE-SINGLE-REQUEST               58200000
           MOVE 00 TO caXreturnXcode IN INQUIRE-SINGLE-REQUEST          58400000
           MOVE CA-ITEM-REF-REQ IN DFHCOMMAREA                          58600000
               TO caXitemXrefXreq IN INQUIRE-SINGLE-REQUEST             58800000
                                                                        59000000
      *---------------------------------------------------------------* 59200000
      * Place the request data into a container on a channel            59400000
      * WS-SERVICE-CONT-NAME   =   'DFHWS-DATA'                         59600000
      * WS-CHANNELNAME         =   'SERVICE-CHANNEL'                    59800000
      *---------------------------------------------------------------* 60000000
           EXEC CICS PUT CONTAINER(WS-SERVICE-CONT-NAME)                60200000
                         CHANNEL(WS-CHANNELNAME)                        60400000
                         FROM(INQUIRE-SINGLE-REQUEST)                   60600000
           END-EXEC                                                     60800000
                                                                        61000000
      *---------------------------------------------------------------* 61200000
      * Build the URI from the server name in the VSAM file and         61400000
      * the local URI for the inquire catalog service                   61600000
      *---------------------------------------------------------------* 61800000
                                                                        62000000
           STRING  'http://'                                            62200000
                   SERVER-LOCATION                                      62400000
                   '/exampleApp/inquireSingle'                          62600000
               DELIMITED BY SPACE                                       62800000
               INTO WS-ENDPOINT-URI                                     63000000
           END-STRING                                                   63200000
                                                                        63400000
      *---------------------------------------------------------------* 63600000
      * Invoke the webservice                                           63800000
      * WS-CHANNELNAME         =   'SERVICE-CHANNEL'                    64000000
      * Operation below changed by PK60058                            * 64100000
      *---------------------------------------------------------------* 64200000
           MOVE 'DFH0XCMNOperation' TO WS-OPERATION                     64400000
                                                                        64600000
           EXEC CICS INVOKE WEBSERVICE('inquireSingleClient')           64800000
                     CHANNEL(WS-CHANNELNAME)                            65000000
                     URI(WS-ENDPOINT-URI)                               65200000
                     OPERATION(WS-OPERATION)                            65400000
                     RESP(WS-RESP) RESP2(WS-RESP2)                      65600000
           END-EXEC.                                                    65800000
                                                                        66000000
      * Check the return code was normal                                66200000
                                                                        66400000
           EVALUATE WS-RESP                                             66600000
               WHEN DFHRESP(NORMAL)                                     66800000
      *            Normal response populate commarea and return         67000000
                                                                        67200000
      *            Get the response container off the channel           67400000
                   EXEC CICS GET CONTAINER(WS-SERVICE-CONT-NAME)        67600000
                             CHANNEL(WS-CHANNELNAME)                    67800000
                             INTO(INQUIRE-SINGLE-RESPONSE)              68000000
                   END-EXEC                                             68200000
                                                                        68400000
                   MOVE caXreturnXcode IN INQUIRE-SINGLE-RESPONSE       68600000
                        TO CA-RETURN-CODE IN DFHCOMMAREA                68800000
                                                                        69000000
                   MOVE caXresponseXmessage IN INQUIRE-SINGLE-RESPONSE  69200000
                        TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA           69400000
                                                                        69600000
      *            Check the return code                                69800000
                   IF caXreturnXcode IN INQUIRE-SINGLE-RESPONSE         70000000
                     EQUAL 00                                           70200000
      *                Response is okay, copy data across to commarea   70400000
                       MOVE caXsingleXitem                              70600000
                               IN INQUIRE-SINGLE-RESPONSE               70800000
                       TO   CA-SINGLE-ITEM IN DFHCOMMAREA               71000000
                   END-IF                                               71200000
                                                                        71400000
               WHEN DFHRESP(INVREQ)                                     71600000
                   MOVE                                                 71800000
                   'Error calling inquire single service - INVREQ'      72000000
                       TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA            72200000
                   MOVE 30 TO CA-RETURN-CODE IN DFHCOMMAREA             72400000
                                                                        72600000
               WHEN DFHRESP(NOTFND)                                     72800000
                   MOVE                                                 73000000
                   'Error calling inquire single service - NOT FOUND'   73200000
                       TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA            73400000
                   MOVE 31 TO CA-RETURN-CODE IN DFHCOMMAREA             73600000
                                                                        73800000
               WHEN OTHER                                               74000000
                   MOVE                                                 74200000
                   'Error calling inquire single service'               74400000
                       TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA            74600000
                   MOVE 32 TO CA-RETURN-CODE IN DFHCOMMAREA             74800000
           END-EVALUATE.                                                75000000
                                                                        75200000
           EXIT.                                                        75400000
                                                                        75600000
      *================================================================*75800000
      * Procedure to make a Web service call to place an order         *76000000
      *   on the catalog data                                          *76200000
      *================================================================*76400000
        PLACE-ORDER.                                                    76600000
      *    Move the data from the input commarea to the                 76800000
      *    'placeOrderRequest' variable ready to put in a container     77000000
           MOVE CA-REQUEST-ID IN DFHCOMMAREA                            77200000
               TO caXrequestXid IN PLACE-ORDER-REQUEST                  77400000
           MOVE 00 TO caXreturnXcode IN PLACE-ORDER-REQUEST             77600000
           MOVE CA-USERID IN DFHCOMMAREA                                77800000
               TO caXuserid IN PLACE-ORDER-REQUEST                      78000000
           MOVE CA-CHARGE-DEPT IN DFHCOMMAREA                           78200000
               TO caXchargeXdept IN PLACE-ORDER-REQUEST                 78400000
           MOVE CA-ITEM-REF-NUMBER IN DFHCOMMAREA                       78600000
               TO caXitemXrefXnumber IN PLACE-ORDER-REQUEST             78800000
           MOVE CA-QUANTITY-REQ IN DFHCOMMAREA                          79000000
               TO caXquantityXreq IN PLACE-ORDER-REQUEST                79200000
                                                                        79400000
                                                                        79600000
      *---------------------------------------------------------------* 79800000
      * Place the request data into a container on a channel            80000000
      * WS-SERVICE-CONT-NAME   =   'DFHWS-DATA'                         80200000
      * WS-CHANNELNAME         =   'SERVICE-CHANNEL'                    80400000
      *---------------------------------------------------------------* 80600000
           EXEC CICS PUT CONTAINER(WS-SERVICE-CONT-NAME)                80800000
                         CHANNEL(WS-CHANNELNAME)                        81000000
                         FROM(PLACE-ORDER-REQUEST)                      81200000
           END-EXEC                                                     81400000
                                                                        81600000
      *---------------------------------------------------------------* 81800000
      * Build the URI from the server name in the VSAM file and         82000000
      * the local URI for the inquire catalog service                   82200000
      *---------------------------------------------------------------* 82400000
                                                                        82600000
           STRING  'http://'                                            82800000
                   SERVER-LOCATION                                      83000000
                   '/exampleApp/placeOrder'                             83200000
               DELIMITED BY SPACE                                       83400000
               INTO WS-ENDPOINT-URI                                     83600000
           END-STRING                                                   83800000
                                                                        84000000
      *---------------------------------------------------------------* 84200000
      * Invoke the webservice                                           84400000
      * WS-CHANNELNAME         =   'SERVICE-CHANNEL'                    84600000
      * Operation below changed by PK60058                            * 84700000
      *---------------------------------------------------------------* 84800000
           MOVE 'DFH0XCMNOperation' TO WS-OPERATION                     85000000
                                                                        85200000
           EXEC CICS INVOKE WEBSERVICE('placeOrderClient')              85400000
                     CHANNEL(WS-CHANNELNAME)                            85600000
                     URI(WS-ENDPOINT-URI)                               85800000
                     OPERATION(WS-OPERATION)                            86000000
                     RESP(WS-RESP) RESP2(WS-RESP2)                      86200000
           END-EXEC.                                                    86400000
                                                                        86600000
      * Check the return code was normal                                86800000
                                                                        87000000
           EVALUATE WS-RESP                                             87200000
               WHEN DFHRESP(NORMAL)                                     87400000
      *            Normal response populate commarea and return         87600000
                                                                        87800000
      *            Get the response container off the channel           88000000
                   EXEC CICS GET CONTAINER(WS-SERVICE-CONT-NAME)        88200000
                             CHANNEL(WS-CHANNELNAME)                    88400000
                             INTO(PLACE-ORDER-RESPONSE)                 88600000
                   END-EXEC                                             88800000
                                                                        89000000
                   MOVE caXreturnXcode IN PLACE-ORDER-RESPONSE          89200000
                        TO CA-RETURN-CODE IN DFHCOMMAREA                89400000
                                                                        89600000
                   MOVE caXresponseXmessage IN PLACE-ORDER-RESPONSE     89800000
                        TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA           90000000
                                                                        90200000
      *            Check the return code                                90400000
                   IF caXreturnXcode IN PLACE-ORDER-RESPONSE            90600000
                     EQUAL 00                                           90800000
      *                Response is okay, copy data across to commarea   91000000
                       MOVE caXorderXrequest                            91200000
                               IN PLACE-ORDER-RESPONSE                  91400000
                       TO   CA-ORDER-REQUEST IN DFHCOMMAREA             91600000
                   END-IF                                               91800000
                                                                        92000000
               WHEN DFHRESP(INVREQ)                                     92200000
                   MOVE                                                 92400000
                   'Error calling place order service - INVREQ'         92600000
                       TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA            92800000
                   MOVE 30 TO CA-RETURN-CODE IN DFHCOMMAREA             93000000
                                                                        93200000
               WHEN DFHRESP(NOTFND)                                     93400000
                   MOVE                                                 93600000
                   'Error calling place order service - NOT FOUND'      93800000
                       TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA            94000000
                   MOVE 31 TO CA-RETURN-CODE IN DFHCOMMAREA             94200000
                                                                        94400000
               WHEN OTHER                                               94600000
                   MOVE                                                 94800000
                   'Error calling place order service'                  95000000
                       TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA            95200000
                   MOVE 32 TO CA-RETURN-CODE IN DFHCOMMAREA             95400000
           END-EVALUATE.                                                95600000
                                                                        95800000
           EXIT.                                                        96000000
                                                                        96200000
                                                                        96400000
      *================================================================*96600000
      * Procedure to handle unknown requests                           *96800000
      *================================================================*97000000
        REQUEST-NOT-RECOGNISED.                                         97200000
           MOVE '99' TO CA-RETURN-CODE IN DFHCOMMAREA                   97400000
                                                                        97600000
           STRING ' UNKNOWN REQUEST ID RECEIVED - '                     97800000
               CA-REQUEST-ID IN DFHCOMMAREA                             98000000
               DELIMITED BY SIZE                                        98200000
               INTO EM-DETAIL                                           98400000
           END-STRING                                                   98600000
                                                                        98800000
           MOVE 'OPERATION UNKNOWN'                                     99000000
               TO CA-RESPONSE-MESSAGE IN DFHCOMMAREA                    99200000
           EXIT.                                                        99400000
                                                                        99600000
                                                                        99800000
